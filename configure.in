dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.59)

dnl ================================================================
dnl Version number definitions
dnl ================================================================
#Note these macros were nabbed from glib and I don't fully understand
# them! I tried to reference the libtool docs and as you can tell from
# my comments below I could not make much sense of it all. (Any help
# would be appreciated :-)
# 
#adapted from the libtool info pages:
#  1. Update the version information only immediately before a public
#     release of your software.  More frequent updates are unnecessary,
#     and only guarantee that the current interface number gets larger
#     faster.
#
#  2. If the library source code has changed at all since the last
#     update, then increment REVISION (`C:R:A' becomes `C:r+1:A').
#     (Where REVISION=gswat_interface_age)
#
#  3. If any interfaces have been added, removed, or changed since the
#     last update, increment CURRENT, and set REVISION to 0.
#     (Where CURRENT=gswat_lt_current which is calculated as
#      100*gswat_minor_version + gswat_micro_version - gswat_interface_age
#        Note: I don't understand why gswat_interface_age is subtracted
#        from gswat_lt_current, but that's what glib + gtk do so I left
#        it there.)
#
#  4. If any interfaces have been added since the last public release,
#     then increment AGE.
#     (Where AGE=(gswat_binary_age - gswat_interface_age) and gswat_binary_age
#      is calculated as:
#      100*gswat_minor_version + gswat_micro_version.
#         Note: It looks to me that ABE also effectlivly = gswat_lt_current which
#         seems to imply continual backwards compatability since you can't
#         zero AGE as required by point 5 below when you remove an
#         interface.
#
#  5. If any interfaces have been removed since the last public release,
#     then set AGE to 0.
#     (Using these glib macros, it looks like you can't do this)
#
m4_define([gswat_major_version], [0])
m4_define([gswat_minor_version], [1])
m4_define([gswat_micro_version], [0])
m4_define([gswat_interface_age], [0])
m4_define([gswat_binary_age],
          [m4_eval(100 * gswat_minor_version + gswat_micro_version)])
m4_define([gswat_version],
          [gswat_major_version.gswat_minor_version.gswat_micro_version])
m4_define([gswat_api_version],
          [gswat_major_version.gswat_minor_version])

# libtool version related macros
m4_define([gswat_lt_release], [gswat_major_version.gswat_minor_version])
m4_define([gswat_lt_current],
          [m4_eval(100 * gswat_minor_version + gswat_micro_version - gswat_interface_age)])
m4_define([gswat_lt_revision], [gswat_interface_age])
m4_define([gswat_lt_age], [m4_eval(gswat_binary_age - gswat_interface_age)])
m4_define([gswat_lt_current_minus_age],
          [m4_eval(gswat_lt_current - gswat_lt_age)])


AC_INIT(gswat, [gswat_version])
AC_CONFIG_SRCDIR(gswat/gswat.h)
AC_CONFIG_MACRO_DIR([m4])
AC_CONFIG_HEADERS(config.h)
AM_INIT_AUTOMAKE([gnu])


dnl ================================================================
dnl More version number stuff
dnl ================================================================
GSWAT_MAJOR_VERSION=gswat_major_version
GSWAT_MINOR_VERSION=gswat_minor_version
GSWAT_MICRO_VERSION=gswat_micro_version
GSWAT_INTERFACE_AGE=gswat_interface_age
GSWAT_BINARY_AGE=gswat_binary_age
GSWAT_VERSION=gswat_version
GSWAT_API_VERSION=gswat_api_version

AC_SUBST(GSWAT_MAJOR_VERSION)
AC_SUBST(GSWAT_MINOR_VERSION)
AC_SUBST(GSWAT_MICRO_VERSION)
AC_SUBST(GSWAT_VERSION)
AC_SUBST(GSWAT_INTERFACE_AGE)
AC_SUBST(GSWAT_BINARY_AGE)
AC_SUBST(GSWAT_API_VERSION)


dnl ================================================================
dnl libtool versioning
dnl ================================================================
LT_RELEASE=gswat_lt_release
LT_CURRENT=gswat_lt_current
LT_REVISION=gswat_lt_revision
LT_AGE=gswat_lt_age
LT_CURRENT_MINUS_AGE=gswat_lt_current_minus_age
AC_SUBST(LT_RELEASE)
AC_SUBST(LT_CURRENT)
AC_SUBST(LT_REVISION)
AC_SUBST(LT_AGE)
AC_SUBST(LT_CURRENT_MINUS_AGE)


AM_MAINTAINER_MODE



dnl ================================================================
dnl Handle extra configure options
dnl ================================================================

AC_ARG_ENABLE(debug,
  AC_HELP_STRING([--enable-debug],
    [enable debuging messages]),
  [
   if test $enableval = yes; then 

     AC_DEFINE(_DEBUG, 1, "define to enable debuging")

     dnl (This is for removing the default -O2 not adding -g)
     CFLAGS="-g -O0"
     EXTRA_CFLAGS="-Wall -Werror"
     SILENT="#"
   else
    if test -z $CFLAGS; then
     CFLAGS="-g -O2"
    fi
     SILENT=".SILENT"
   fi
  ],
  [
   if test -z $CFLAGS; then
        CFLAGS="-g -O2"
   fi
   SILENT=".SILENT"
  ])


AC_ARG_ENABLE(werror,
  AC_HELP_STRING([--enable-werror],
    [Use -Werror option for compilation]),
  [
      if test $enableval = yes; then 
         EXTRA_CFLAGS="-Wall -Werror"
      else
         EXTRA_CFLAGS="-Wall"
      fi
  ], 
  [
   if test -z $EXTRA_CFLAGS; then
      EXTRA_CFLAGS="-Wall"
   fi
  ])

AC_ARG_ENABLE(uninstalled-build,
              AC_HELP_STRING([--enable-uninstalled-build],
			     [Search for files in build directory as well]),
	      enable_uninstalled=yes,)
if test x"$enable_uninstalled" = xyes; then
	AC_DEFINE_UNQUOTED(
        SHARE_UNINSTALLED_DIR,"`pwd`/data",[path to source data dir])
	AC_DEFINE_UNQUOTED(
        METADATA_UNINSTALLED_DIR,"`pwd`/metadata",[path to metadata build dir])
fi


dnl ================================================================
dnl Compiler stuff.
dnl ================================================================
AM_PROG_CC_C_O
AC_ISC_POSIX
AC_C_CONST


dnl ================================================================
dnl Libtool stuff.
dnl ================================================================
AC_PROG_LIBTOOL
LIBTOOL="$LIBTOOL --preserve-dup-deps"


dnl ================================================================
dnl I18n stuff.
dnl ================================================================
AC_PROG_INTLTOOL([0.23])
GETTEXT_PACKAGE=gswat
AC_SUBST(GETTEXT_PACKAGE)
AC_SUBST([CONFIG_STATUS_DEPENDENCIES],['$(top_srcdir)/po/LINGUAS'])
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE", [Gettext package])
ALL_LINGUAS="`cat "$srcdir/po/LINGUAS" | grep -v '^#'`"
gswatlocaledir='${prefix}/${DATADIRNAME}/locale'
AC_SUBST(gswatlocaledir)
AM_GLIB_GNU_GETTEXT


dnl ================================================================
dnl Documentation stuff.
dnl ================================================================
GTK_DOC_CHECK(1.11)
GNOME_DOC_INIT


dnl ================================================================
dnl Check for dependency packages.
dnl ================================================================
#TODO once we have seperated out the gui code for gswat-session.c
# we will be able to remove most of these deps.
PKG_CHECK_MODULES(LIBGSWAT_DEP, [
		  glib-2.0 >= 2.16 
                  gio-2.0
		  gthread-2.0
                  libxml-2.0
])

PKG_CHECK_MODULES(GSWAT_SERVER_DEP, [
		  glib-2.0 >= 2.2 
])
PKG_CHECK_MODULES(GSWAT_PTRACE_STUB_DEP, [
		  glib-2.0 >= 2.2 
])


dnl ================================================================
dnl Misc program dependencies.
dnl ================================================================
AC_PATH_PROG(GLIB_GENMARSHAL, glib-genmarshal)
AC_PROG_INSTALL


dnl ================================================================
dnl Checks for header files.
dnl ================================================================
AC_PATH_X
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h limits.h unistd.h signal.h)


dnl ================================================================
dnl Checks for library functions.
dnl ================================================================
AC_TYPE_SIGNAL
AC_CHECK_FUNCS(putenv strdup)


dnl ================================================================
dnl What needs to be substituted in other files
dnl ================================================================
AC_SUBST(EXTRA_CFLAGS)
AC_SUBST(EXTRA_CPPFLAGS)
AC_SUBST(LIBGSWAT_DEP_CFLAGS)
AC_SUBST(LIBGSWAT_DEP_LIBS)
AC_SUBST(GSWAT_SERVER_DEP_LIBS)
AC_SUBST(GSWAT_SERVER_DEP_CFLAGS)
AC_SUBST(GSWAT_PTRACE_STUB_DEP_LIBS)
AC_SUBST(GSWAT_PTRACE_STUB_DEP_CFLAGS)


dnl ================================================================
dnl Dah Da!
dnl ================================================================
AC_OUTPUT(
Makefile 
gswat/Makefile
tests/Makefile
po/Makefile.in
gswat-0.1.pc
doc/Makefile
doc/reference/version.xml
doc/reference/Makefile
)

